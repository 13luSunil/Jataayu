// Top-level build file where you can add configuration options common_build to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.61'

    repositories {
        google()
        jcenter()
        gradlePluginPortal()
    }

    dependencies {
        classpath Deps.toolsAndroidStudio
        classpath Deps.toolsKotlinGradle
        classpath Deps.toolsGradleVersions
        classpath Deps.toolsJacocoCore
        classpath Deps.toolsDicemelonJacoco
        classpath 'com.palantir:jacoco-coverage:0.4.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            apply plugin: "com.github.ben-manes.versions"

            //<editor-fold desc="android common DSL">
            android {
                buildToolsVersion Config.buildToolsVersion
                compileSdkVersion Config.compileSdkVersion

                defaultConfig {
                    minSdkVersion Config.minSdkVersion
                    targetSdkVersion Config.compileSdkVersion

                    testInstrumentationRunner "in.org.projecteka.jataayu.provider.JataayuJUnitRunner"
                }

                compileOptions {
                    sourceCompatibility Config.javaVersion
                    targetCompatibility Config.javaVersion
                }

                signingConfigs {
                    release {
                        storeFile file(String.valueOf(System.getenv("UPLOAD_KEY_PATH")))
                        storePassword System.getenv("UPLOAD_KEY_PASSWORD")
                        keyAlias System.getenv("UPLOAD_KEY")
                        keyPassword System.getenv("UPLOAD_KEY_PASSWORD")
                    }
                }

                buildTypes {
                    debug {
                        testCoverageEnabled true
                        debuggable true
                        minifyEnabled false
                    }
                    qa {
                        minifyEnabled true
                        debuggable true
                        signingConfig = signingConfigs.release
                    }
                    release {
                        testCoverageEnabled false
                        minifyEnabled true
                        debuggable true
                        signingConfig = signingConfigs.release
                    }
                }

                //<editor-fold desc="Source Sets DSL">
                sourceSets {
                    main.java.srcDirs += 'src/main/java'
                    test.java.srcDirs += 'src/test/java'
                    androidTest.java.srcDirs += 'src/androidTest/java'

                    String sharedTestJavaDir = 'src/sharedTest/java'
                    String sharedTestResourcesDir = 'src/sharedTest/resources'

                    test {
                        java.srcDirs += sharedTestJavaDir
                        resources.srcDirs += sharedTestResourcesDir
                    }
                    androidTest {
                        java.srcDirs += sharedTestJavaDir
                        resources.srcDirs += sharedTestResourcesDir
                    }
                }
                //</editor-fold>

                dataBinding {
                    enabled = true
                    enabledForTests = true
                }

                kotlin {
                    experimental {
                        coroutines "enable"
                    }
                }

                testOptions {
                    unitTests.returnDefaultValues = true
                }
            }

            kapt {
                correctErrorTypes = true
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
